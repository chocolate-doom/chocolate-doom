AC_INIT(Chocolate Doom, 1.0.0, fraggle@gmail.com, chocolate-doom)

AC_CONFIG_AUX_DIR(autotools)

orig_CFLAGS="$CFLAGS"

AC_PROG_CC
AC_PROG_RANLIB
AC_CHECK_PROG(HAVE_PYTHON, python, true, false)

OPT_LEVEL=2

# Engine room, we need more speed!

AC_ARG_ENABLE(penis-extension, 
[  --enable-penis-extension   Enable counterproductive compiler optimisations ],
[ OPT_LEVEL=3 ])

# If this is gcc, we have some options we'd like to turn on.  Turn on 
# optimisation and debugging symbols.

if test "$GCC" = "yes"
then
        CFLAGS="-O$OPT_LEVEL -g -Wall $orig_CFLAGS"
fi

AC_CHECK_HEADERS([linux/kd.h dev/isa/spkrio.h])

AM_PATH_SDL(1.1.3)

AC_CHECK_LIB(SDL_mixer,Mix_LoadMUS,[
dnl    AC_DEFINE(HAVE_LIBSDL_MIXER)
    SDLMIXER_LIBS="$SDLMIXER_LIBS -lSDL_mixer"
],[
    echo "*** Could not find SDL_mixer.  Please install it."
    exit -1
],$SDL_LIBS $SDLMIXER_LIBS)

AC_CHECK_LIB(SDL_net,SDLNet_UDP_Send,[
    SDLNET_LIBS="$SDLNET_LIBS -lSDL_net"
],[
    echo "*** Could not find SDL_net.  Please install it."
    exit -1
] ,$SDL_LIBS $SDLNET_LIBS)

AC_CHECK_TOOL(WINDRES, windres, )

AM_CONDITIONAL(HAVE_WINDRES, test "$WINDRES" != "")
AM_CONDITIONAL(HAVE_PYTHON, $HAVE_PYTHON)

dnl Automake v1.8.0 is required, please upgrade!

AM_INIT_AUTOMAKE([1.8.0])

WINDOWS_RC_VERSION=`echo $PACKAGE_VERSION.0 | sed 's/\./, /g' `

AM_CONFIG_HEADER(config.h:config.hin)

AC_SUBST(WINDOWS_RC_VERSION)
AC_SUBST(SDLMIXER_CFLAGS)
AC_SUBST(SDLMIXER_LIBS)

AC_SUBST(SDLNET_CFLAGS)
AC_SUBST(SDLNET_LIBS)

AC_SUBST(ac_aux_dir)

dnl Shut up the datarootdir warnings.
AC_DEFUN([AC_DATAROOTDIR_CHECKED])

AC_OUTPUT([
Makefile
textscreen/Makefile
textscreen/examples/Makefile
setup/Makefile
man/Makefile
src/Makefile
pcsound/Makefile
src/resource.rc
src/doom-screensaver.desktop
setup/setup-res.rc
])

