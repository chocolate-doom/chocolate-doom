AC_INIT(Chocolate Doom, 0.2.0, fraggle@alkali.org, chocolate-doom)
AC_CONFIG_AUX_DIR(autotools)

orig_CFLAGS="$CFLAGS"

AC_PROG_CC
AC_PROG_RANLIB

if test "$GCC" = "yes"
then
        CFLAGS="-O3 $orig_CFLAGS -g"
fi

AM_PATH_SDL(1.1.3)

# Always build a console mode app

SDL_LIBS=`echo $SDL_LIBS | sed 's/mwindows/mconsole/'`

AC_CHECK_LIB(SDL_mixer,Mix_LoadMUS,[
dnl    AC_DEFINE(HAVE_LIBSDL_MIXER)
    SDLMIXER_LIBS="$SDLMIXER_LIBS -lSDL_mixer"
],echo "*** Compiling without SDL_mixer installed probably won't work"
,$SDL_LIBS $SDLMIXER_LIBS)

AC_CHECK_LIB(SDL_net,SDLNet_UDP_Send,[
    SDLNET_LIBS="$SDLNET_LIBS -lSDL_net"
],echo "*** Compiling without SDL_net installed probably won't work"
,$SDL_LIBS $SDLNET_LIBS)

AC_CHECK_TOOL(WINDRES, windres, )

AM_CONDITIONAL(HAVE_WINDRES, test "$WINDRES" != "")

AM_INIT_AUTOMAKE(AC_PACKAGE_TARNAME,AC_PACKAGE_VERSION)

WINDOWS_RC_VERSION=`echo $PACKAGE_VERSION.0 | sed 's/\./, /g' `

AM_CONFIG_HEADER(config.h:config.hin)

AC_SUBST(WINDOWS_RC_VERSION)
AC_SUBST(SDLMIXER_CFLAGS)
AC_SUBST(SDLMIXER_LIBS)

AC_SUBST(SDLNET_CFLAGS)
AC_SUBST(SDLNET_LIBS)

AC_SUBST(ac_aux_dir)

AC_C_BIGENDIAN

AC_OUTPUT([
Makefile
textscreen/Makefile
textscreen/examples/Makefile
setup/Makefile
man/Makefile
src/Makefile
src/chocolate-doom-res.rc
])

